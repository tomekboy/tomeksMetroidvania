class_name PlayerStateStun extends PlayerState

@export var move_speed : float = 100
@export var invulnerable_duration : float = 0.5
@export var audio : AudioStream

var time : float = 0.0
var dir : float = 1.0

@onready var damage_area: DamageArea = %DamageArea

# What happens when this state is initialized?
func init() -> void:
	damage_area.damage_taken.connect( _on_damage_taken )
	pass


# What happens when we enter this state?
func enter() -> void:
	player.animation_player.play( "stun" )
	# set timer
	time = player.animation_player.current_animation_length
	# make invulnerable
	damage_area.make_invulnerable( invulnerable_duration )
	# play audio
	AudioManager.play_spatial_sound( audio, player.global_position )
	# screen shake
	VisualEffects.camera_shake( 2.0 )
	pass


# What happens when we exit this state?
func exit() -> void:
	pass


# What happens when an input is pressed?
func handle_input( _event : InputEvent ) -> PlayerState:
	return null


# What happens each process tick in this state?
func process( delta: float ) -> PlayerState:
	time -= delta
	if time <= 0:
		if player.hp <= 0:
			return death
		return idle
	return null


# What happens each physics_process tick in this state?
func physics_process( _delta: float ) -> PlayerState:
	player.velocity.x = move_speed * dir
	return null


func _on_damage_taken( attack_area : AttackArea ) -> void:
	if player.current_state == death:
		return
	player.change_state( self )
	if attack_area.global_position.x < player.global_position.x:
		dir = 1.0
	else:
		dir = -1.0
	pass
