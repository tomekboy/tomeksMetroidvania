class_name Player extends CharacterBody2D

signal damage_taken

#region /// on ready variables
@onready var sprite: PlayerSprite = $Sprite2D
@onready var attack_sprite: Sprite2D = %AttackSprite2D
@onready var collision_stand: CollisionShape2D = $CollisionStand
@onready var collision_crouch: CollisionShape2D = $CollisionCrouch
@onready var da_stand: CollisionShape2D = %DAStand
@onready var da_crouch: CollisionShape2D = %DACrouch
@onready var one_way_platform_shape_cast: ShapeCast2D = $OneWayPlatformShapeCast
@onready var animation_player: AnimationPlayer = $AnimationPlayer
@onready var attack_area: AttackArea = %AttackArea
@onready var damage_area: DamageArea = %DamageArea
@onready var wall_climb_right_raycast: RayCast2D = %WallClimbRightRaycast
@onready var wall_climb_left_raycast: RayCast2D = %WallClimbLeftRaycast
@onready var wall_climb_timer: Timer = $WallClimbTimer
#endregion

#region /// export variables
@export_category( "Movement" )
@export var move_speed : float = 125.0
@export var max_fall_velocity : float = 600.0
#endregion

#region /// climb variables
@export_category( "Wall Jump / Climb" )
@export var wall_slide_velocity : float = 10
@export var wall_x_force : float = 200.0
@export var wall_y_force : float = -550.0
var is_wall_jumping = false
#endregion

#region /// state machine variables
var states : Array[ PlayerState ]
var current_state : PlayerState :
	get : return states.front()
var previous_state : PlayerState :
	get : return states[ 1 ]
#endregion

#region /// player stats 
var hp : float = 25 :
	set( value ):
		hp = clampf( value, 0, max_hp )
		MessageManager.player_health_changed.emit( hp, max_hp )

var max_hp : float = 50 :
	set( value ):
		max_hp = value
		MessageManager.player_health_changed.emit( hp, max_hp )
#endregion

#region /// standard variables
var direction : Vector2 = Vector2.ZERO
var gravity : float = 980
var gravity_multiplier : float = 1.0
var wall_direction : Vector2 = Vector2.ZERO
#endregion

#region /// abilities
var dash : bool = false
var dash_count : int = 0
var double_jump : bool = false
var jump_count : int = 0
var ground_slam : bool = false
var morph_roll : bool = false
var can_interact : bool = false
#endregion


func _ready() -> void:
	if get_tree().get_first_node_in_group( "Player" ) != self:
		self.queue_free()
	initialize_states()
	self.call_deferred( "reparent", get_tree().root )
	MessageManager.player_healed.connect( _on_player_healed )
	MessageManager.back_to_title_screen.connect( queue_free )
	damage_area.damage_taken.connect( _on_demage_taken )
	hp = max_hp
	pass


func _unhandled_input( event: InputEvent ) -> void:
	if event.is_action_released( "jump" ) and velocity.y < 0:
		velocity.y *= 0.5
	if event.is_action_pressed( "action" ):
		MessageManager.player_interacted.emit( self )
	elif event.is_action_pressed( "pause" ):
		get_tree().paused = true
		var pause_menu : PauseMenu = load( "res://pause_menu/pause_menu.tscn" ).instantiate()
		add_child( pause_menu )
		return
		
	change_state( current_state.handle_input( event ) )
	pass


func _process( delta: float ) -> void:
	update_direction()
	change_state( current_state.process( delta ) )
	pass


func _physics_process( delta: float ) -> void:
	velocity.y += gravity * delta * gravity_multiplier
	velocity.y = clampf( velocity.y, -1000, max_fall_velocity )
	move_and_slide()
	change_state( current_state.physics_process( delta ) )
	pass


func initialize_states() -> void:
	states = []
	# gather all the states
	for c in $States.get_children():
		if c is PlayerState:
			states.append( c )
			c.player = self
		pass
	
	if states.size() == 0:
		return
	
	# initialize all states
	for state in states:
		state.init()
		
	# set our first state
	change_state( current_state )
	current_state.enter()
	pass


func change_state( new_state : PlayerState ) -> void:
	if new_state == null:
		return
	elif new_state == current_state:
		return
	
	if current_state:
		current_state.exit()
	
	states.push_front( new_state )
	current_state.enter()
	states.resize( 3 )
	pass


func update_direction() -> void:
	var prev_direction : Vector2 = direction
	# eliminate stick drift to happen
	var x_axis = Input.get_axis( "left", "right" )
	var y_axis = Input.get_axis( "up", "down" )
	if is_wall_jumping == false:
		direction = Vector2( x_axis, y_axis )
		
		if prev_direction.x != direction.x:
			attack_area.flip( direction.x )
			if direction.x < 0: # left
				sprite.flip_h = true
				attack_sprite.flip_h = true
				attack_sprite.position.x = -24
			elif direction.x > 0:
				sprite.flip_h = false
				attack_sprite.flip_h = false
				attack_sprite.position.x = 24
	pass


func _on_player_healed( amount : float ) -> void:
	hp += amount
	pass


func _on_demage_taken( a : AttackArea ) -> void:
	if current_state == PlayerStateDeath:
		return
	# reduce hp
	hp -= a.damage
	# emit signal
	damage_taken.emit()
	pass


func can_dash() -> bool:
	if dash == false or dash_count > 0:
		return false
	return true

func can_morph() -> bool:
	if morph_roll == false:
		return false
	return true

func can_wall_climb() -> bool:
	return wall_climb_timer.is_stopped() and is_on_wall_only() and ( wall_climb_right_raycast.is_colliding() or wall_climb_left_raycast.is_colliding() )
